
    model{
    #phi ~ dunif(0,1) # Annual survival prob
    alpha0 ~ dnorm(0,0.37)
    alpha1 ~ dnorm(0,0.37)

    for(t in 1:2){
      pFyke[t]<-0 # det prob zero during years when fyke was not used
    }                                        
    for(t in 3:n.occ){
      p.d[(t-2)] ~ dunif(0,1) # Pr(catch in fyke net)      #### FIRST 3 PLACES IN p.d RESERVED FOR RANDOM pFyke VALUES
      pFyke[t] <- p.d[(t-2)]
    }
    
    for(t in 1:3){
      p.d[(t+3)] ~ dunif(0,1)                              #### SLOTS 4-6 IN p.d RESERVED FOR RANDOM pSeine VALUES
      pSeine[t] <- p.d[(t+3)]
    }
    for(t in 4:n.occ){
      pSeine[t] <-0 # det prob zero during years when seine was not used
    }
    
    for(t in 1:3){
      pPit[t]<-0 # det prob zero during years when PIT antenna was not set up
    }
    for(t in 4:n.occ){
      p.d[(t+3)] ~ dunif(0,1)                              #### SLOTS 7-8 IN p.d RESERVED FOR RANDOM pPit VALUES
      pPit[t] <- p.d[(t+3)]
    }

    phi.male <- 1/(1+exp(-(alpha0+alpha1)))   # back-transformed male survival rates
    phi.female <- 1/(1+exp(-(alpha0)))   # back-transformed female survival rates
    
    for (i in 1:nind){
    logit(phi[i]) <- alpha0 + alpha1*Sex[i]
    }

 
    for(t in 1:n.occ){
    cap.probs[1,t,1] <- (1-pSeine[t])*(1-pFyke[t])*(1-pPit[t]) # Pr(not detected|alive)
    cap.probs[1,t,2] <- pSeine[t]*(1-pFyke[t])*(1-pPit[t]) # Pr(detected in seine, but not by other gear)
    cap.probs[1,t,3] <- pFyke[t]*(1-pSeine[t])*(1-pPit[t])
    cap.probs[1,t,4] <- pPit[t]*(1-pFyke[t])*(1-pSeine[t])
    cap.probs[1,t,5] <- pFyke[t]*pPit[t]*(1-pSeine[t])
    cap.probs[1,t,6] <- pSeine[t]*pFyke[t]*(1-pPit[t])
    cap.probs[1,t,7] <- pSeine[t]*pPit[t]*(1-pFyke[t])
    cap.probs[1,t,8] <- pSeine[t]*pFyke[t]*pPit[t]
    cap.probs[2,t,1] <- 1 # If dead, can't be detected
    cap.probs[2,t,2] <- 0
    cap.probs[2,t,3] <- 0
    cap.probs[2,t,4] <- 0
    cap.probs[2,t,5] <- 0
    cap.probs[2,t,6] <- 0
    cap.probs[2,t,7] <- 0
    cap.probs[2,t,8] <- 0
    }

    for (i in 1:nind){
    z[i, f[i]] <- 1 # Known to be alive in first capture year
    for (t in (f[i]+1):n.occ){
      z[i,t] ~ dbern(z[i,t-1]*phi[i]) # Actual alive/dead state (partially observed)
      state[i,t] <- ifelse(z[i,t]<1, 2, 1)
      y[i,t] ~ dcat(cap.probs[state[i,t],t,1:8])
    }
    }
    
    for(t in 2:n.occ){
      pdot[t]<-1-(1-pFyke[t])*(1-pPit[t])*(1-pSeine[t]) # det prob of being detected by at least one type of gear
      N[t]<-n[t]/pdot[t] # estimated pop size 
    }
    }
    
    
